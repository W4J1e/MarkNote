plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

ext.kotlin_version = '1.9.22'

android {
    namespace 'me.shouheng.notepal.app'
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "me.shouheng.notepal"
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.targetSdkVersion
        versionCode 13
        versionName "2.3"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
            UMENG_CHANNEL_VALUE: "umeng",
            UMENG_APPKEY: "${umengAppKey}",
            FABRIC_APPKEY: "${fabricAppKey}",
        ]
    }

    signingConfigs {
        release {
            keyAlias System.getenv("_keyAlias") ?: "${_keyAlias}"
            storeFile file(System.getenv("_storeFile") ?: "${_storeFile}")
            storePassword System.getenv("_storePassword") ?: "${_storePassword}"
            keyPassword System.getenv("_keyPassword") ?: "${_keyPassword}"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ["-Xjvm-default=all"]
    }

    buildFeatures {
        dataBinding true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    flavorDimensions "default"
    productFlavors {
        dev { dimension "default" }
        alpha { dimension "default" }
        beta { dimension "default" }
        prod { dimension "default" }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    
    implementation project(path: ':commons')
    implementation project(path: ':data')
    implementation project(path: ':pinlockview')
    implementation project(path: ':fingerprint')
}